buildscript {
	repositories {
		jcenter()
	}
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

repositories {
    jcenter()
    maven {
        name = "apache"
        url = "http://repo.maven.apache.org/maven2"
    }
}

group = 'com.error22.karonda-vm'
version = '1.0.0'

dependencies {
    api 'org.ow2.asm:asm-debug-all:5.2'
    implementation 'com.google.guava:guava:21.0'
    implementation 'org.javatuples:javatuples:1.2'
    implementation 'it.unimi.dsi:fastutil:8.1.1'
    
    testImplementation 'junit:junit:4.12'
}


def pomConfig = {
    licenses {
        license {
            name "GNU Lesser General Public License v3.0"
            url "https://raw.githubusercontent.com/Error22/Karonda/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "csnewman"
            name "Chandler Newman"
            email "chandler2newman@hotmail.co.uk"
        }
    }
    
    scm {
       url "https://github.com/Error22/Karonda"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            group = 'com.error22.karonda-vm'
            version = '1.0.0'
            artifactId 'karonda-vm'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A non-optimising step based Java VM')
                root.appendNode('name', 'Karonda VM')
                root.appendNode('url', 'https://github.com/Error22/Karonda')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {	
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['MyPublication']
	pkg {
		repo = 'error22-maven'
		name = 'karonda-vm'
		userOrg = user
		licenses = ['LGPL-3.0']
		vcsUrl = 'https://github.com/Error22/Karonda.git'
		version {
			name = '1.0.0'
			desc = 'Karonda VM 1.0.0'
			vcsTag = '1.0.0'
		}	
	}
}